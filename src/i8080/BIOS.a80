;
;**************************************************************
;*
;*        B I O S   J U M P   T A B L E
;*
;**************************************************************
;
    ORG 0FE00H
;
BOOT	JMP	0	;NOTE WE USE FAKE DESTINATIONS
WBOOT	JMP	0
CONST	JMP	TTST
CONIN	JMP	TTIN
CONOUT	JMP	TTOUT
LIST	JMP	PRNOUT
PUNCH	JMP	NOTIMP
READER	JMP	RETEOF
HOME	JMP	FDHOME
SELDSK	JMP	0
SETTRK	JMP	FDTRK
SETSEC	JMP	FDSEC
SETDMA	JMP	FDDMA
READ	JMP	FDRD
WRITE	JMP	FDWR
PRSTAT	JMP	NOTIMP
SECTRN	JMP	TRNSEC
;
;  I/O Ports
;
TTYDAT  EQU 0   ; Simple console device at ports 0 & 1
TTYST   EQU 1
PRNDAT  EQU 2   ; Simple printer at port 2
PFDCTL  EQU 3   ; Floppy control port
PFDSEC  EQU PFDCTL+1 ; Select sector number
PFDTRK  EQU PFDCTL+2 ; Select track number
PFDLSB  EQU PFDCTL+3 ; LSB of DMA address
PDFMSB  EQU PFDCTL+4 ; MSB of DMA address
;
;  Implementation of BIOS functions
;
;  Return console status (A and flags are impacted).
;  A = 00H - No data
;  A = FFH - Data ready
TTST    IN TTYST
        ANI 1       ; Test input status bit
        JNZ TTST1
        RET
TTST1   MVI A,0FFH
        RET
;
;  Wait for an input character and return it in A
TTIN    IN TTYST
        ANI 1
        JZ TTIN     ; Wait for status bit to be 1
        IN TTYDAT
        RET
;
;  Write character in C to console output
TTOUT   PUSH PSW
        MOV A,C
        OUT TTYDAT
        POP PSW
        RET
;
;  Write character in C to printer port
PRNOUT  PUSH PSW
        MOV A,C
        OUT PRNDAT
        POP PSW
        RET
;
;  Move selected disk to the home track (0)
FDHOME  PUSH PSW
        XRA A
        OUT PFDTRK
        POP PSW
        RET
;
;  Select FD disk.  Drive number is in C, return address of DPH in HL.
;  Register E bit 0 = 1 if the disk has been logged in before
FDSEL   PUSH PSW
        MOV A,C
        CPI 0       ; Check for disk 0
        JNZ DSK1
        ORI 0C0H
        OUT PFDCTL
        LXI H,DPH0
        JMP DSKX
DSK1    CPI 1       ; Check for disk 1
        JNZ DSK2
        ORI 0C0H
        OUT PFDCTL
        LXI H,DPH1
        JMP DSKX
DSK2    CPI 2       ; Check for disk 2
        JNZ DSK3
        ORI 0C0H
        OUT PFDCTL
        LXI H,DPH2
        JMP DSKX
DSK3    CPI 3       ; Check for disk 3
        JNZ DSK4
        ORI 0C0H
        OUT PFDCTL
        LXI H,DPH3
        JMP DSKX
DSK4    LXI H,0     ; Unknown disk
DSKX    POP PSW
        RET
;
;  Select FD track (range 0-255 in BC - only C used)
FDTRK   PUSH PSW
        MOV A,C
        OUT PFDTRK
        POP PSW
        RET
;
;  Select FD sector (range 0-255 in BC - only C used)
FDSEC   PUSH PSW
        MOV A,C
        OUT PFDTRK
        POP PSW
        RET
;
;  Set the DMA address for the next disk read/write
FDDMA   PUSH PSW
        MOV A,C
        OUT PFDLSB
        MOV A,B
        OUT PDFMSB
        POP PSW
        RET
;
;  Read from the floppy disk
FDRD    PUSH PSW
        MVI A,040H
        OUT PFDCTL
        POP PSW
        RET
;
;  Write to the floppy disk
FDWR    PUSH PSW
        MVI A,080H
        OUT PFDCTL
        POP PSW
        RET

;
;  Do sector translation.  Currently no skewing is supported so simply
;  copy BC into HL
TRNSEC  MOV H,B
        MOV L,C
        RET
;
;  Output devices that are not implemented simply return
NOTIMP  RET
;
;  Input devices that are not implemented return ^Z (EOF).
RETEOF  MVI A,1AH
        RET
;
;  Data tables for disks
;
; Disk parameter block (same for all disks)
;
DPB0    DW  77      ; Number of tracks per disk
        DB  3       ; Block shift (1K)?
        DB  7       ; Block mask (1K)?
        DB  7       ; Extent mask?
        DW  (26*77)/8-1 ; Number of blocks on disk (250) - 1
        DW  127     ; Number of directory entries - 1
        DB  0F0H    ; First byte of directory allocation bitmap
        DB  00H     ; Second byte of directory allocation bitmap
        DW  32      ; Checksum vector size
        DW  2       ; Number of reserved tracks
;
; Disk parameter headers
;
DPH0    DW 0        ; Address translation table
        DW 0,0,0    ; Workspace for CP/M
        DW DIRBUF   ; Address of sector buffer
        DW DPB0     ; Address of DPB
        DW CHSMV0   ; Address of checksum vector
        DW ALOCV0   ; Address of allocation vector
;
;  Checksum and allocation vectors
;

CHSMV0  DS 32       ; Checksum vector
ALOCV0  DS 32       ; Allocation vector
;
DPH1    DW 0        ; Address translation table
        DW 0,0,0    ; Workspace for CP/M
        DW DIRBUF   ; Address of sector buffer
        DW DPB0     ; Address of DPB
        DW CHSMV1   ; Address of checksum vector
        DW ALOCV1   ; Address of allocation vector
;
;  Checksum and allocation vectors
;

CHSMV1  DS 32       ; Checksum vector
ALOCV1  DS 32       ; Allocation vector
;
DPH2    DW 0        ; Address translation table
        DW 0,0,0    ; Workspace for CP/M
        DW DIRBUF   ; Address of sector buffer
        DW DPB0     ; Address of DPB
        DW CHSMV2   ; Address of checksum vector
        DW ALOCV2   ; Address of allocation vector
;
;  Checksum and allocation vectors
;

CHSMV2  DS 32       ; Checksum vector
ALOCV2  DS 32       ; Allocation vector
;
DPH3    DW 0        ; Address translation table
        DW 0,0,0    ; Workspace for CP/M
        DW DIRBUF   ; Address of sector buffer
        DW DPB0     ; Address of DPB
        DW CHSMV3   ; Address of checksum vector
        DW ALOCV3   ; Address of allocation vector
;
;  Checksum and allocation vectors
;

CHSMV3  DS 32       ; Checksum vector
ALOCV3  DS 32       ; Allocation vector
;
;  128 Byte buffer for all disks
;
DIRBUF  DS 128
;
;*
;******************   E N D   O F   C P / M   *****************
;*
    END
